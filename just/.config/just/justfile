default:
  just -g --list

drive_share_to_download_link share_link:
  #!/usr/bin/env python3

  import sys

  share_link = "{{share_link}}"

  print("Share link:", share_link, file=sys.stderr)

  if not share_link.startswith("https://drive.google.com/file/d/"):
      print("Invalid share link.", file=sys.stderr)
      sys.exit(1)
  file_id = share_link.split("/")[5]
  download_link = f"https://drive.google.com/uc?id={file_id}&export=download"

  print(download_link, end="")

get_deepl_link lang *args:
  #!/usr/bin/env python3

  import sys
  import urllib.parse

  language = "{{lang}}"
  text = "{{args}}"

  print(f"Language: {language}", file=sys.stderr)
  print(f"Text: {text}", file=sys.stderr)

  url = "https://www.deepl.com/en/translator#"
  url += f"{language}/en/"
  url += urllib.parse.quote(text)

  if not sys.stdout.isatty():
      print(f"Piping URL to stdout: {url}", file=sys.stderr)

  print(url, end="")

test_darwin:
  test "$(uname)" == "Darwin"

reset_dock: test_darwin
  defaults write com.apple.dock tilesize -int 32
  killall Dock

show_desktop: test_darwin
  defaults write com.apple.finder CreateDesktop true
  killall Finder

hide_desktop: test_darwin
  defaults write com.apple.finder CreateDesktop false
  killall Finder

charger_watts: test_darwin
  ioreg -r -c AppleSmartBattery | grep -i "AppleRawAdapterDetails" | sed "s/.*\"Watts\"=\([0-9]*\).*/\1/" | xargs echo -n

flush_dns_cache: test_darwin
  sudo dscacheutil -flushcache
  sudo killall -HUP mDNSResponder

lock: test_darwin
  open -a ScreenSaverEngine

thermals: test_darwin
  sudo powermetrics -s thermal

keychain:
  keychain --nogui -q $HOME/.ssh/id_ed25519

ipify:
  curl -s https://api.ipify.org

utcnow:
  date -u +"%Y-%m-%dT%H:%M:%SZ" | xargs echo -n

utcnowsafe:
  date -u +"%Y-%m-%dT%H%M%SZ" | xargs echo -n
